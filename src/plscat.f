      SUBROUTINE PLSCAT(FILE,NEW,LR,KIN,LLOST,ITYPE,FSEL,
     %   FUNX,FUNY,XYMM,LOGHV,MAXNP,
     %   LASER,NLSR,LTS,TS,TITLE,TITLEX,TITLEY,
     %   ICOLOR,MSGFL)
	USE FLCHTYP
	USE BEAMCM
      IMPLICIT NONE
      INTEGER FILE,NEW,LR(2),KIN(3),LLOST,ITYPE(3),LOGHV(2),
     %    MAXNP,LASER,NLSR,LTS,ICOLOR,MSGFL
      CHARACTER*(*) FSEL,FUNX,FUNY
      REAL*8 XYMM(2,2),TS
      CHARACTER*(*) TITLE,TITLEX,TITLEY
      INTEGER I,N,NC0(2,3),NC(3),J,NN,IRTN,NC1,LSR,NP1,NP10,NP11,ISTAT
      INTEGER NPCOUNT,IFLRKN
      REAL*8 PWEIGHT,RANDCAIN
	EXTERNAL RANDCAIN
      REAL*8 XYM(2,2),XY1,PROB,PROB1,WGT1,DUMMY(1),SPIN1(3)
	TYPE(FLCHTYPE) F1,F2,FSEL1
      REAL*8 XYW0(2,2)/0.0,13.0,0.0,10.0/,XYW(2,2)/0.0,10.8,0.0,10.0/
      CHARACTER*256 TTL(2,3)
      CHARACTER*1 SEP/';'/
      CHARACTER*256 TEXT
	REAL(8), ALLOCATABLE:: TXYS1(:,:),EP1(:,:),WORK1(:,:)
      INTEGER IDLM(2),IDLMSEL
C      INCLUDE 'include/beamcm.h'
C
	IF(LASER.LE.0) THEN
        NN=NPCOUNT(1,LR,KIN,0,0,LLOST,ITYPE,FSEL)
        IF(NN.LE.0) GOTO 900
	ENDIF
      CALL EVCMPL(FUNX,IDLM(1),TEXT)
      IF(TEXT.NE.' ') GOTO 910
      CALL EVCMPL(FUNY,IDLM(2),TEXT)
      IF(TEXT.NE.' ') GOTO 920
	IDLMSEL=0
	IF(LASER.LE.0) THEN
	  IF(FSEL.NE.' ') THEN
	    CALL EVCMPL(FSEL,IDLMSEL,TEXT)
	    IF(TEXT.NE.' ') GOTO 950
	  ENDIF
	ENDIF
      CALL RDTTL(TITLE,2,TTL(1,1),NC0(1,1),SEP)
      CALL RDTTL(TITLEX,2,TTL(1,2),NC0(1,2),SEP)
      CALL RDTTL(TITLEY,2,TTL(1,3),NC0(1,3),SEP)
      DO 200 I=1,3
        NC(I)=NC0(1,I)
 200  CONTINUE
	IF(LASER.GT.0) THEN
	  NP10=MAXNP/NLSR
	  IF(NP10.LE.10) NP10=1000
	  NP1=NP10*NLSR
	  ALLOCATE(TXYS1(0:3,NP1),EP1(0:3,NP1),WORK1(2,NP1),STAT=ISTAT)
	  IF(ISTAT.NE.0) GOTO 970
	  SPIN1=0
	  NP1=0
	  DO LSR=1,NLSR
	    CALL LSRPHOTON(LSR,LTS,TS,NP10,TXYS1(0,NP1+1),EP1(0,NP1+1),
     %          NP11,IRTN)
	    IF(IRTN.GE.2) GOTO 960
	    NP1=NP1+NP11
	  ENDDO
	ELSE
	  ALLOCATE(WORK1(2,NP),STAT=ISTAT)
	  IF(ISTAT.NE.0) GOTO 970
        IF(MAXNP.GE.1.AND.NN.GT.MAXNP) THEN
          PROB=DFLOAT(MAXNP)/DFLOAT(NN)
        ELSE
          PROB=1
        ENDIF
	  NP1=NP
	ENDIF
      NN=0
      DO 220 N=1,NP1
	  IF(LASER.GT.0) THEN
	    CALL SETVAR2(1,1,TXYS1(0,N),EP1(0,N),SPIN1,1D0,PNAME(N))
	  ELSE
          J=IFLRKN(N,LR,KIN,0,0,LLOST,ITYPE)
          IF(J.EQ.0) CYCLE
          WGT1=PWEIGHT(N)
          IF(WGT1.EQ.0) CYCLE
          IF(PROB.NE.1) THEN
            PROB1=RANDCAIN()
            IF(PROB1.GT.PROB) CYCLE
          ENDIF
          CALL SETVAR(N,IRTN)
          IF(IRTN.NE.0) CYCLE
	    IF(IDLMSEL.NE.0) THEN
	      CALL EVLOAD(IDLMSEL,FSEL1,IRTN)
	      IF(FSEL1%L.NE.1.OR.IRTN.NE.0) CYCLE
	      IF(FSEL1%X.EQ.0) CYCLE
	    ENDIF
	  ENDIF
        CALL EVLOAD(IDLM(1),F1,IRTN)
        IF(IRTN.EQ.0.AND.F1%L.EQ.1) THEN
          CALL EVLOAD(IDLM(2),F2,IRTN)
          IF(IRTN.EQ.0.AND.F2%L.EQ.1) THEN
            NN=NN+1
            WORK1(1,NN)=F1%X
            WORK1(2,NN)=F2%X
          ENDIF
        ENDIF
 220  CONTINUE
	IF(NN.LE.1) RETURN
      DO 280 I=1,2
      IF(XYMM(1,I).EQ.UNDEF%X.OR.XYMM(2,I).EQ.UNDEF%X) THEN
        XYM(1,I)=1D60
        XYM(2,I)=-1D60
        DO 240 N=1,NN
          XYM(1,I)=MIN(XYM(1,I),WORK1(I,N))
          XYM(2,I)=MAX(XYM(2,I),WORK1(I,N))
 240    CONTINUE
        IF(XYM(1,I).EQ.XYM(2,I)) THEN
          IF(XYM(1,I).EQ.0) THEN
            XYM(1,I)=-1
            XYM(2,I)=1
          ELSE
            XYM(1,I)=XYM(1,I)-0.5*ABS(XYM(1,I))
            XYM(2,I)=XYM(2,I)+0.5*ABS(XYM(2,I))
          ENDIF
        ELSE
          XY1=XYM(2,I)-XYM(1,I)
          XYM(1,I)=XYM(1,I)-0.05*XY1
          XYM(2,I)=XYM(2,I)+0.05*XY1
        ENDIF
        IF(LOGHV(I).GE.1) THEN
          IF(XYM(2,I).LE.0) GOTO 940
          XYM(1,I)=MAX(XYM(1,I),XYM(2,I)/1D3)
        ENDIF
        XYMM(1,I)=XYM(1,I)
        XYMM(2,I)=XYM(2,I)
      ELSE
        IF(LOGHV(I).GE.1.AND.
     %       (XYMM(1,I).LE.0.OR.XYMM(2,I).LE.0)) GOTO 930
        XYM(1,I)=XYMM(1,I)
        XYM(2,I)=XYMM(2,I)
      ENDIF
 280  CONTINUE
      CALL SCAT(FILE,NEW,0,DUMMY,ICOLOR,NN,WORK1,XYW,XYM,LOGHV,TTL,NC)
      GOTO 1000

 900  CALL TYPTTL(LR,KIN,LLOST,ITYPE,TEXT,NC1)
      WRITE(MSGFL,905) TEXT(1:NC1),FSEL
 905  FORMAT(' (SUBR.PLSCAT) No particles satisfying the condition:',
     %    /,10X,A,1X,A)
      RETURN
 910  WRITE(MSGFL,915) FUNX,TEXT(1:80)
 915  FORMAT(' (SUBR.PLSCAT) Invalid function "',A,'".',/,5X,A)
      GOTO 1000
 920  WRITE(MSGFL,925) FUNY,TEXT(1:80)
 925  FORMAT(' (SUBR.PLSCAT) Invalid function "',A,'".',/,5X,A)
      GOTO 1000
 930  WRITE(MSGFL,935)
 935  FORMAT(' (SUBR.PLSCAT) Nonpositive axis range for log scale ',
     %   'scatter plot.')
      GOTO 1000
 940  WRITE(MSGFL,945)
 945  FORMAT(' (SUBR.PLSCAT) All data nonpositive for log scale ',
     %   'scatter plot.')
      GOTO 1000
 950  WRITE(MSGFL,955) FSEL,TEXT(1:80)
 955  FORMAT(' (SUBR.PLSCAT) Invalid SELECT function "',A,'".',/,5X,A)
      GOTO 1000
 960  WRITE(MSGFL,965)
 965  FORMAT(' (SUBR.PLSCAT) Failed in generating laeser photons.')
      GOTO 1000
 970  WRITE(MSGFL,975)
 975  FORMAT(' (SUBR.PLSCAT) Allocation failed.')
      GOTO 1000
1000	IF(IDLM(2).GE.1) CALL EVLMFREE(IDLM(2))
	IF(IDLM(2).GE.1) CALL EVLMFREE(IDLM(2))
	IF(IDLMSEL.GE.1) CALL EVLMFREE(IDLMSEL)
	DEALLOCATE(WORK1,STAT=ISTAT)
	IF(LASER.GE.1) DEALLOCATE(TXYS1,EP1,STAT=ISTAT)
	RETURN
      END
