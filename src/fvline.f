C******************** FVLINE *******************************************
      SUBROUTINE FVLINE(F,IC,MX,NX,NY,FV,IR,X,Y,ID,MP,NP,LP,IER,MSG)
C  FIND LINES ON (X,Y)-PLANE ON WHICH FUNCTION F(X,Y)=FV.
C  INPUT:
C   IC       1 (REAL FUNC)   2(COMPLEX FUNC)
C   MX       First dimension of F. (0 to MX)
C   F(I,J) (0<=I<=NX,0<=J<=NY)  FUNCTION VALUES ON GRID POINTS.
C            IF IC=1, REAL*4, IF IC=2, COMPLEX*8 ARRAY.
C   NX,NY    (# OF GRID LINES IN EACH DIRECTION) -1 .
C   FV       FUNCTION VALUE TO BE SEARCHED. FOR IC=2 SEE BELOW.
C   IR       CASE IC=2 ONLY. IF IR=1, FV=REAL(F). IF 2, IMAG(F).
C            IF 3, ABS(F). IF 4, ARG(F) (DEGREE).
C            IN THE CASE IR=4, THIS ROUTINE TRACES ZERO-LINE OF
C            IMAG(F*EXP(-I*FV)). HENCE FV MAKES SENSE UP TO MOD
C            180 DEGREE.
C   MP       DIMENSION OF X,Y AND ID. Possible max of NP is 2*NX*NY+NX+NY
C   MSG      File # for message
C  OUTPUT:
C   (X(N),Y(N)) (N=1,NP)  COORDINATES OF THE POINTS WHERE F=FV. GRID
C            INTERVAL UNIT. (E.G. X=0 AT LEFT EDGE AND X=NX AT RIGHT)
C   ID(N) (N=1,NP)  INDICATES THE RELATION BETWEEN THE POINTS.
C            IF ID(N1)>0, ID(N2)<0 AND ID(N)=0 (N1<N<N2), THEN (X(N),
C            Y(N) (N=N1,..N2) MAKE A CONNECTED LINE IN THIS ORDER.
C            IF ID(N1)=5 ( IN THIS CASE ID(N2)=-5), IT IS A CLOSED LOOP.
C            (I.E., X(N1)=X(N2), Y(N1)=Y(N2)). IF 1<= ABS(ID(N)) <=4
C            (N=N1 OR N2), THE LINE ENDS ON A EDGE. (IF =1, ON THE LOWER
C            X-AXIS. IF =2, RIGHT Y-AXIS. IF =3, UPPER X-AXIS. IF =4,
C            LEFT Y-AXIS.)
C   NP       NUMBER OF THE POINTS FOUND.
C   IER      0 (NORMAL). 1 ('MP' IS INSUFFICIENT. MP POINTS ARE STORED)
C  WORK AREA:
C   LP      LENGTH >= 2*(NX+1)*(NY+1) BYTES.
C  IF FV>FMAX OR FV<FMIN, AVOID CALLING THIS ROUTINE TO SAVE TIME.
C
      IMPLICIT NONE
      INTEGER IC,MX,NX,NY,IR,MP,NP,ID(MP),IER,MSG
      REAL*8 F(IC,0:MX,0:NY),FV,X(MP),Y(MP)
      LOGICAL*1 LP(0:NX,0:NY,2)
C
      INTEGER IY,IX,IRTN
      REAL*8 FV1,CO,SI
      LOGICAL*1 LP0,LP1
      REAL*8 FVLIN1
      REAL*8 PI/3.141592653589793238D0/
C
C  LP(IX,IY,KXY)=.T. IF F=FV ON THE LINE SEGMENT <IX,IY,KXY>.
C  LINE SEGMENTS ARE NUMBERED AS FOLLOWS.
C    KXY=1  PARARELL TO X-AXIS.  =2  PARARELL TO Y-AXIS.
C    (IX,IY) IS THE GRID POINT NUMBER OF THE RIGHT(KXY=1) OR
C           UPPER(KXY=2) END OF THE SEGMENT.
C  HENCE, <0,IY,1> AND <IX,0,2> HAVE NO CORRESPONDING LINE SEGMENT.
      FV1=FV
      IF(IC.NE.2) GOTO 100
      IF(IR.LE.0.OR.IR.GE.5) GOTO 940
      IF(IR.EQ.3) FV1=FV1**2
      IF(IR.NE.4) GOTO 110
      FV1=MOD(FV1,180.0D0)*PI/180.0
      CO=COS(FV1)
      SI=SIN(FV1)
      FV1=0.0
      GOTO 110
  100 IF(IC.NE.1) GOTO 920
  110 NP=0
      DO 122 IY=0,NY
      LP1=FVLIN1(F(1,0,IY),IC,IR,CO,SI).GE.FV1
      DO 120 IX=1,NX
      LP0=LP1
      LP1=FVLIN1(F(1,IX,IY),IC,IR,CO,SI).GE.FV1
      LP(IX,IY,1)=LP1.NEQV.LP0
 120  CONTINUE
 122  CONTINUE
      DO 142 IX=0,NX
      LP1=FVLIN1(F(1,IX,0),IC,IR,CO,SI).GE.FV1
      DO 140 IY=1,NY
      LP0=LP1
      LP1=FVLIN1(F(1,IX,IY),IC,IR,CO,SI).GE.FV1
      LP(IX,IY,2)=LP1.NEQV.LP0
 140  CONTINUE
 142  CONTINUE
C
      DO 200 IX=1,NX
         IF(LP(IX, 0,1)) THEN
            CALL TRACE(IX, 0,1,2,F,IC,MX,NX,NY,FV1,IR,
     %           CO,SI,X,Y,ID,MP,NP,LP,IRTN)
            IF(IRTN.NE.0) GOTO 900
         ENDIF
c       print *, " fvline point 001 ix,irtn= ",ix,irtn
 200  CONTINUE
      DO 210 IY=1,NY
         IF(LP(NX,IY,2)) THEN
            CALL TRACE(NX,IY,2,3,F,IC,MX,NX,NY,FV1,IR,
     %           CO,SI,X,Y,ID,MP,NP,LP,IRTN)
c            print *, " fvline point 002 iy,irtn= ",iy,irtn
            IF(IRTN.NE.0) GOTO 900
         ENDIF
 210  CONTINUE
      DO 220 IX=NX,1,-1
         IF(LP(IX,NY,1)) THEN
            CALL TRACE(IX,NY,1,4,F,IC,MX,NX,NY,FV1,IR,
     %           CO,SI,X,Y,ID,MP,NP,LP,IRTN)
c            print *, " fvline point 003 ix,irtn= ",ix,irtn
            IF(IRTN.NE.0) GOTO 900
         ENDIF
 220  CONTINUE
      DO 230 IY=NY,1,-1
         IF(LP( 0,IY,2)) THEN
            CALL TRACE( 0,IY,2,1,F,IC,MX,NX,NY,FV1,IR,
     %           CO,SI,X,Y,ID,MP,NP,LP,IRTN)
c            print *, " fvline point 004 iy,irtn= ",iy,irtn
            IF(IRTN.NE.0) GOTO 900
         ENDIF
 230  CONTINUE
      DO 242 IY=1,NY-1
      DO 240 IX=1,NX
         IF(LP(IX,IY,1)) THEN
            CALL TRACE(IX,IY,1,2,F,IC,MX,NX,NY,FV1,IR,
     %           CO,SI,X,Y,ID,MP,NP,LP,IRTN)
c            print *, " fvline point 005 ix,iy,irtn= ",ix,iy,irtn
            IF(IRTN.NE.0) GOTO 900
         ENDIF
 240  CONTINUE
 242  CONTINUE
      IER=0
      RETURN
  900 WRITE(MSG,910) IRTN,NX,NY,MP,NP
  910 FORMAT(' (SUBR.FVLINE) BUFFER INSUFFICIENT.',/,
     %    '    IRTN,NX,NY,MP,NP=',i5,4I12)
      GOTO 990
  920 WRITE(MSG,930) IC
  930 FORMAT(' (SUBR.FVLINE) INPUT ERROR. IC MUST BE 1 OR 2.',
     %   ' YOU PUT',I4)
      GOTO 990
  940 WRITE(MSG,950) IR
  950 FORMAT(' (SUBR.FVLINE) INPUT ERROR. IR MUST BE 1 TO 4.',
     %   ' YOU PUT',I4)
  990 IER=1
      RETURN
      END
